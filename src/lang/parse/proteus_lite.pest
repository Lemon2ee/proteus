WHITESPACE = _{ " " | "\t" | NEWLINE }

identifier = @{ ASCII_ALPHA~ASCII_ALPHANUMERIC* }
name_value = { "NAME" ~ ":" ~ identifier }
type_value = { "TYPE" ~ ":" ~ (primitive_type | array) }

field = { "{" ~ name_value ~ ";" ~ type_value ~ "}" }

size_of_op = ${ identifier~".size_of" }

primitive_array = { "[" ~ primitive_type ~ ";" ~ positive_numeric_literal ~ "]" }
dynamic_array = { "[" ~ "u8" ~ ";" ~ size_of_op ~ "]" }
array = { primitive_array | dynamic_array }

numeric_type = { "u8" | "u16" | "u32" | "u64" | "i8" | "i16" | "i32" | "i64" }
primitive_type = { numeric_type | "bool" | "char" }

numeric_literal = @{ "-"?~positive_numeric_literal }
positive_numeric_literal = @{ ASCII_DIGIT+ }
typed_numeric_literal = @{ numeric_literal~numeric_type }

string_literal = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

format = { "DEFINE" ~ identifier ~ "FIELDS" ~ field ~ ("," ~ field)* ~ ";" }

field_semantic = { "padding" | "payload" | "length" }

sematic_binding = { "{" ~
  "FORMAT" ~ ":" ~ identifier ~ ";" ~
  "FIELD" ~ ":" ~ identifier ~ ";" ~
  "SEMANTIC" ~ field_semantic ~ "}" }

psf = { SOI ~ "@SEGMENT FORMATS" ~ format+ ~ "@SEGMENT SEMANTICS" ~ EOI }
